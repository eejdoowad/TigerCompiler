tiger-program -> LET #p-prog-p-decs declaration-segment #a-decs-p-stats IN stat-seq #a-stats END
declaration-segment -> #p-types type-declaration-list #a-types-p-vars var-declaration-list #a-vars-p-funs funct-declaration-list #a-funs
type-declaration-list -> EPSILON
type-declaration-list -> type-declaration type-declaration-list
var-declaration-list -> EPSILON
var-declaration-list -> var-declaration var-declaration-list
funct-declaration-list -> EPSILON
funct-declaration-list -> funct-declaration funct-declaration-list
type-declaration -> #p-type TYPE ID #p-newtypeID EQ type SEMI #p-addtype
type -> type-id #p-newtype
type -> ARRAY LBRACK INTLIT #p-newtypedim RBRACK OF type-id #p-newtype
type -> ID
type-id -> INT
type-id -> FLOAT
var-declaration -> VAR #p-var id-list COLON type optional-init SEMI
id-list -> ID #b-varid id-list-tail
id-list-tail -> COMMA ID #b-varid id-list-tail
id-list-tail -> EPSILON
optional-init -> EPSILON
optional-init -> ASSIGN const #b-constinit
const -> INTLIT
const -> FLOATLIT
funct-declaration -> FUNC ID LPAREN param-list RPAREN ret-type BEGIN stat-seq END SEMI
param-list -> EPSILON
param-list -> param param-list-tail
param-list-tail -> EPSILON
param-list-tail -> COMMA param param-list-tail
ret-type -> EPSILON
ret-type -> COLON type
param -> ID COLON type
stat-seq -> stat stat-seq-tail
stat-seq-tail -> stat stat-seq-tail
stat-seq-tail -> EPSILON
stat -> #p-if IF expr THEN stat-seq stat-if-tail SEMI #a-if
stat -> WHILE expr DO stat-seq ENDDO SEMI
stat -> FOR ID ASSIGN expr TO expr DO stat-seq ENDDO SEMI
stat -> BREAK SEMI
stat -> RETURN expr SEMI
stat -> ID #p-ID stat-id-tail SEMI
stat-id-tail -> #p-assign ASSIGN stat-assign-tail
stat-id-tail -> #p-assign LBRACK expr RBRACK ASSIGN stat-assign-tail
stat-id-tail -> LPAREN expr-list RPAREN
stat-assign-tail -> LPAREN term0 RPAREN term5' term4' term3' term2' term1' term0'
stat-assign-tail -> const #b-const term5' term4' term3' term2' term1' term0'
stat-assign-tail -> ID stat-assign-id-tail
stat-assign-id-tail -> LPAREN expr-list RPAREN
stat-assign-id-tail -> opt-index term5' term4' term3' term2' term1' term0'
stat-if-tail -> ENDIF
stat-if-tail -> ELSE stat-seq ENDIF
expr -> term0
term0 -> term1 term0'
term0' -> OR term1 term0'
term0' -> EPSILON
term1 -> term2 term1'
term1' -> AND term2 term1'
term1' -> EPSILON
term2 -> term3 term2'
term2' -> GREATER term3 term2'
term2' -> LESSER term3 term2'
term2' -> GREATEREQ term3 term2'
term2' -> LESSEREQ term3 term2'
term2' -> EPSILON
term3 -> term4 term3'
term3' -> EQ term4 term3'
term3' -> NEQ term4 term3'
term3' -> EPSILON
term4 -> term5 term4'
term4' -> PLUS term5 term4'
term4' -> MINUS term5 term4'
term4' -> EPSILON
term5 -> term6 term5'
term5' -> MULT term6 term5'
term5' -> DIV term6 term5'
term5' -> EPSILON
term6 -> LPAREN term0 RPAREN
term6 -> const
term6 -> ID opt-index
opt-index -> LBRACK expr RBRACK
opt-index -> EPSILON
expr-list -> EPSILON
expr-list -> expr expr-list-tail
expr-list-tail -> COMMA expr expr-list-tail
expr-list-tail -> EPSILON